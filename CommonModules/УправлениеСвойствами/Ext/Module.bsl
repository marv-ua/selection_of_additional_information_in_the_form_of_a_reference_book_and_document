
&После("ПриСозданииНаСервере")
Процедура Затр_ПриСозданииНаСервере(Форма, ДополнительныеПараметры)
	
	// вывести доп сведения в форму как доп реквизит
	ДобавляемыеРеквизиты = Новый Массив;
	
	СписокСвойств = УправлениеСвойствами.ПолучитьСписокСвойств(ДополнительныеПараметры.Объект.Ссылка, Ложь, Истина);
	Для Каждого Свойство Из СписокСвойств Цикл   
		
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(Свойство.Имя, Свойство.ТипЗначения, "", Свойство.Заголовок, Ложь));
		
	КонецЦикла;	
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ГруппаФормы = Затр_ГруппаДопСвойств(Форма);
	Для Каждого Свойство Из СписокСвойств Цикл
		
		ЗначенияСвойства = УправлениеСвойствами.ЗначенияСвойств(ДополнительныеПараметры.Объект.Ссылка, 
			Ложь, 
			Истина, 
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Свойство),
		);
		Если НЕ ЗначенияСвойства.Количество() = 0 Тогда
			Форма[Свойство.Имя] = ЗначенияСвойства[0].Значение;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	С.Ссылка КАК Значение
		|ИЗ
		|	Справочник.ЗначенияСвойствОбъектов КАК С
		|ГДЕ
		|	С.Владелец = &Свойство
		|	И НЕ С.ПометкаУдаления";
		Запрос.УстановитьПараметр("Свойство", Свойство);
		ТаблицаДоступныхЗначенийСвойства = Запрос.Выполнить().Выгрузить();
		
		НовоеПолеФормы = Форма.Элементы.Добавить(Свойство.Имя, Тип("ПолеФормы"), ГруппаФормы);
		НовоеПолеФормы.ПутьКДанным = Свойство.Имя;
		НовоеПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
		НовоеПолеФормы.КнопкаСоздания = Ложь;
		НовоеПолеФормы.РежимВыбораИзСписка = Истина;
		НовоеПолеФормы.СписокВыбора.ЗагрузитьЗначения(ТаблицаДоступныхЗначенийСвойства.ВыгрузитьКолонку("Значение"));
		НовоеПолеФормы.УстановитьДействие("ПриИзменении", "Затр_Подключаемый_ПриИзмененииДополнительногоСведения");
		
	КонецЦикла;	
	
КонецПроцедуры

Функция Затр_ГруппаДопСвойств(Форма) 
	
	Форма.Элементы.ГруппаДополнительныеРеквизиты.Видимость = Истина;
	Возврат Форма.Элементы.ГруппаДополнительныеРеквизиты;
	
КонецФункции	